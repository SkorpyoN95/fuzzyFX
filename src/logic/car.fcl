FUNCTION_BLOCK

VAR_INPUT
car_speed : REAL;
surface: REAL;
line: REAL;
experience: REAL;
END_VAR

VAR_OUTPUT
acceleration: REAL;
END_VAR

FUZZIFY car_speed
TERM slow := (0, 1)(30, 1)(50, 0);
TERM moderate := (40, 0)(50, 1)(70, 1)(80, 0);
TERM fast := (70, 0)(100, 1)(120, 1);
END_FUZZIFY

FUZZIFY surface
TERM icy := (0, 1)(0.2, 1)(0.45, 0);
TERM loose := (0.35, 0)(0.5, 1)(0.6, 1)(0.75, 0);
TERM dry := (0.6, 0)(0.8, 1)(1, 1);
END_FUZZIFY

FUZZIFY line
TERM turn := (-90, 1)(-70, 1)(-40, 0)(40,0)(70,1)(90,1);
TERM curve := (-65, 0)(-45, 1)(-25, 1)(-10, 0)(10, 0)(25, 1)(45, 1)(65, 0);
TERM straight := (-30, 0)(-15, 1)(15, 1)(30, 0);
END_FUZZIFY

FUZZIFY experience
TERM beginner := (0, 1)(25, 1)(60, 0);
TERM advanced := (25, 0)(60, 1)(75, 1)(95, 0);
TERM pro := (80, 0)(95, 1)(100, 1);
END_FUZZIFY

DEFUZZIFY acceleration
TERM break := (-10, 1)(-5, 1)(0, 0);
TERM still := (-3, 0)(-1, 1)(1, 1)(3, 0);
TERM accelerate := (0, 0)(5, 1)(10, 1);
METHOD : COG;
DEFAULT := 0;
END_DEFUZZIFY

RULEBLOCK ruleset
AND: MIN;
ACT: MIN;

RULE 0: IF experience IS beginner AND car_speed IS NOT slow THEN acceleration IS break;
RULE 1: IF experience IS advanced AND car_speed IS fast THEN acceleration IS break;
RULE 2: IF line IS turn AND car_speed IS moderate AND experience IS NOT pro THEN acceleration IS break;
RULE 3: IF surface IS icy AND car_speed IS NOT slow AND experience IS NOT pro THEN acceleration IS break WITH 0.5;
RULE 4: IF surface IS icy AND car_speed IS fast AND experience IS pro THEN acceleration IS break;
RULE 5: IF line IS turn AND surface IS loose AND car_speed IS fast AND experience IS pro THEN acceleration IS break;
RULE 6: IF line IS turn AND surface IS loose AND car_speed IS moderate AND experience IS advanced THEN acceleration IS break;
RULE 7: IF line IS curve AND surface IS loose AND car_speed IS moderate AND experience IS advanced THEN acceleration IS break;
RULE 8: IF car_speed IS slow AND line IS NOT turn AND experience IS NOT beginner AND surface IS dry THEN acceleration IS accelerate;
RULE 9: IF car_speed IS slow AND line IS NOT turn AND experience IS pro AND surface IS loose THEN acceleration IS accelerate;
RULE 10: IF car_speed IS moderate AND line IS NOT turn AND experience IS pro AND surface IS NOT icy THEN acceleration IS accelerate;
RULE 11: IF car_speed IS slow AND experience IS beginner THEN acceleration IS still;
RULE 12: IF car_speed IS moderate AND experience IS advanced AND line IS NOT turn AND surface IS NOT icy THEN acceleration IS still;

END_RULEBLOCK

END_FUNCTION_BLOCK